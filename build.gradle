plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "maven-publish"
    alias libs.plugins.quilt.loom
}

base {
    archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/${project.mod_id}.accesswidener")

    mods {
        "etched" {
            sourceSet("main")
        }
    }
}

repositories {
    mavenLocal()
//    maven {
//        // location of the maven that hosts JEI files since January 2023
//        name = "Jared's maven"
//        url = "https://maven.blamejared.com/"
//    }
//    maven {
//        // location of a maven mirror for JEI files, as a fallback
//        name = "ModMaven"
//        url = "https://modmaven.dev"
//    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://mvn.devos.one/snapshots/" }
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

dependencies {
    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
    }

    modImplementation libs.quilt.loader
    modImplementation libs.quilted.fabric.api
    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forge_cofig_api_version}"
    modLocalRuntime "fuzs.forgeconfigscreens:forgeconfigscreens-fabric:${forge_cofig_api_version}"
    modLocalRuntime 'com.terraformersmc:modmenu:7.1.0'
    modImplementation(include("com.tterrag.registrate_fabric:Registrate:${project.registrate_version}"))

    shadowCommon(implementation("javazoom:jlayer:1.0.1"))
}

processResources {
    inputs.property "mod_id", project.mod_id
    inputs.property "mod_version", project.mod_version
    inputs.property "mod_name", project.mod_name
    inputs.property "mod_description", project.mod_description
    inputs.property "archives_base_name", project.archives_base_name

    inputs.property "minecraft_version", project.minecraft_version

    filesMatching('quilt.mod.json') {
        expand 'version': version, 'group': project.group
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    relocate "javazoom", "gg.moonflower.etched.javazoom"
    archiveClassifier = "dev-shadow"
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    from('LICENSE') {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}